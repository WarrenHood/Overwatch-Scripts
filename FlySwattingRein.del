globalvar define currentTime;

rule: "ScalePlayers"
Event.OngoingPlayer {
    for (;;) {
        if (EventPlayer().Hero() == Hero.Reinhardt || EventPlayer().Hero() == Hero.Brigitte) {
            StartScalingPlayer(EventPlayer(), 2.0);
            //DisableMovementCollisionWithEnvironment(EventPlayer());
            SetMoveSpeed(EventPlayer(), 150);
        }
        else {
            StartScalingPlayer(EventPlayer(), 0.3);
            //EnableMovementCollisionWithEnvironment(EventPlayer());
            if (EventPlayer().Hero() == Hero.Tracer) {
                SetMoveSpeed(EventPlayer(), 250);
            }
            else {
                SetMoveSpeed(EventPlayer(), 200);
            }
            
        }
        Wait(1);
    }
}

rule: "FasterReinCharge"
Event.OngoingPlayer {
    for (;;) {
        if (EventPlayer().Hero() == Hero.Reinhardt) {
            if (IsUsingAbility1(EventPlayer())) {
                SetMoveSpeed(EventPlayer(), 500);
            }
            else {
                SetMoveSpeed(EventPlayer(), 150);
            }
        }
        Wait(0.25); // Only wait if not reinhardt
    }
}

rule: "ScaleDamage" 
Event.OnDamageDealt {
    if (EventPlayer().Hero() == Hero.Reinhardt || EventPlayer().Hero() == Hero.Brigitte) {
        SetDamageDealt(EventPlayer(), 100.0);
    }
    else {
        SetDamageDealt(EventPlayer(), 100.0/CountOf(AllPlayers(Team.Team2)));
    }
}

rule: "Death points"
Event.OnDeath {
    if (EventPlayer().Team() == Team.Team2) {
        if (IsDummyBot(EventPlayer())) {
            BigMessage(AllPlayers(Team.Team1), "Squished bug (Bot) for an additional 10 seconds");
            currentTime += 10;
        }
        else {
            BigMessage(AllPlayers(Team.Team1), "Squished bug (Human) for an additional 20 seconds");
            currentTime += 20;
        }
    }
}

rule: "Match Timer" 
Event.OngoingGlobal {
    currentTime = 40; // + (20.0 * CountOf(AllPlayers(Team.Team2)));
    define hudText = CreateHudText(AllPlayers(Team.Team1), "Squish the bugs!", "Time Remaining", currentTime, Location.Top, -1, Color.Blue, Color.Red, Color.White);
    define hudText2 = CreateHudText(AllPlayers(Team.Team2), "Try to survive!", "Time Remaining", currentTime, Location.Top, -1, Color.Blue, Color.Red, Color.White);
    define hudText3 = CreateHudText(AllPlayers(), "Fly Swatting Simulator", null, null, Location.Right, -1, Color.Red, Color.Red, Color.Red);
    define lastRein = null;
    define matchEnded = false;
    define swapSides = true;

    DisableCompletion();
    DisableScoring();

    for (;;) {
        DisableCompletion();
        DisableScoring();
        if (matchEnded) {
            if (swapSides) {
                 BigMessage(AllPlayers(), "Swapping sides");

                // Move rein or brig to first free slot
                if (!IsDummyBot(AllPlayers(Team.Team1)[0])) {
                    for(define i=0; i < NumberOfSlots(Team.Team2); i++) {
                        define currentPlayer = PlayersInSlot(i, Team.Team2);
                        if (currentPlayer == null || IsDummyBot(currentPlayer)) {
                            MovePlayerToTeam(PlayersInSlot(0, Team.Team1), Team.Team2, i);
                            break;
                        }
                    }
                }

                // Move someone to the rein/brig team
                define nextRein = RandomValueInArray(AllPlayers(Team.Team2));
                define attemptsRemaining = 5;
                while ((nextRein == lastRein || nextRein == null || IsDummyBot(nextRein)) && attemptsRemaining >= 0) {
                    nextRein = RandomValueInArray(FilteredArray(AllPlayers(Team.Team2), !IsDummyBot(ArrayElement())));
                    attemptsRemaining--;
                }
                if (nextRein != lastRein) {
                    MovePlayerToTeam(nextRein, Team.Team1, 0);
                }
                
            }
           
            matchEnded = false;
            RestartMatch();
        }
        
        /*
        if (matchEnded && (!swapSides ||  lastRein != PlayersInSlot(0, Team.Team1) || lastRein == null)) {
            // If the rein/brig from last time is different, we can restart
            matchEnded = false;
            RestartMatch();
        }*/

        if (!matchEnded && IsGameInProgress() && CountOf(FilteredArray(AllLivingPlayers(Team.Team2), HasSpawned(ArrayElement()))) == 0 && CountOf(AllPlayers(Team.Team1)) > 0 && CountOf(AllPlayers(Team.Team2)) > 0) {
            // Reinhardt/Brig wins
            lastRein = AllPlayers(Team.Team1)[0];
            matchEnded = true;

            BigMessage(AllPlayers(Team.Team1), "You win!");
            BigMessage(AllPlayers(Team.Team2), "You lose!");
            Wait(1);
        }

        /*
        if (MatchRound() > 1) {
            if (!matchEnded && CountOf(AllPlayers(Team.Team1)) > 0 && CountOf(AllPlayers(Team.Team2)) > 0) {
                lastRein = AllPlayers(Team.Team1)[0];
                matchEnded = true;
            }
            RestartMatch();
        }*/
        DisableGameModeHud(AllPlayers());
        if (IsGameInProgress() && !matchEnded) {
            //SetMatchTime(currentTime+1);
            PauseMatchTime();
            DestroyHudText(hudText);
            DestroyHudText(hudText2);
            DestroyHudText(hudText3);
            hudText = CreateHudText(AllPlayers(Team.Team1), "Squish the bugs", "Time Remaining", currentTime, Location.Top, -1, Color.Blue, Color.Red, Color.White);
            hudText2 = CreateHudText(AllPlayers(Team.Team2), "Try to survive!", "Time Remaining", currentTime, Location.Top, -1, Color.Blue, Color.Red, Color.White);
            hudText3 = CreateHudText(AllPlayers(), "Fly Swatting Simulator", null, null, Location.Right, -1, Color.Red, Color.Red, Color.Red);
            if (currentTime <= 0 || CountOf(FilteredArray(AllLivingPlayers(Team.Team1), HasSpawned(ArrayElement()))) == 0) {
                // Bugs/Flies win
                currentTime = 40 + (20.0 * CountOf(AllPlayers(Team.Team2)));
                BigMessage(AllPlayers(Team.Team1), "You failed to squish the bugs in time!");
                BigMessage(AllPlayers(Team.Team2), "You win!");
                
                lastRein = AllPlayers(Team.Team1)[0];
                matchEnded = true;
            }
            currentTime--;
        }
        else {
            currentTime = 40;// + (20.0 * CountOf(AllPlayers(Team.Team2)));
        }
        Wait(1);
    }
}

rule: "Spawn_Bot"
Event.OngoingGlobal {
    define reinbot = null;
    define useRein = true;
    define roachCount = 5;
    define flyCount = 6;

    for (;;) {
        if (useRein) {
            if (CountOf(AllPlayers(Team.Team1)) == 0) {
                reinbot = CreateDummyBot(Hero.Reinhardt, Team.Team1, 0);
                StartForcingDummyBotName(reinbot, "ReinBot");
            }
        }
        // Spawn roaches
        if (CountOf(AllPlayers(Team.Team2)) < roachCount + flyCount) {
            for (define i=0; i < roachCount; i++) {
                for(define iSlot=0; iSlot<NumberOfSlots(Team.Team2); iSlot++) {
                    if (PlayersInSlot(iSlot, Team.Team2) == null) {
                        StartForcingDummyBotName(CreateDummyBot(Hero.Tracer, Team.Team2, iSlot), "Roach");
                        break;
                    }
                }
            }
        }
        // Spawn flies
        if (CountOf(AllPlayers(Team.Team2)) < roachCount + flyCount) {
            for (define i=0; i < flyCount; i++) {
                for(define iSlot=0; iSlot<NumberOfSlots(Team.Team2); iSlot++) {
                    if (PlayersInSlot(iSlot, Team.Team2) == null) {
                        StartForcingDummyBotName(CreateDummyBot(Hero.Pharah, Team.Team2, iSlot), "Fly");
                        break;
                    }
                }
            }
        }
        Wait(0.25);
    }
}

rule: "Bot_Actions" 
Event.OngoingPlayer {
    define rein_inaccuracy_range = 1.5;
    for (;;) {
        if (TeamOf(EventPlayer()) == Team.Team1 && !IsGameInProgress() && !IsWaitingForPlayers()) {
            DisallowButton(EventPlayer(), Button.Ability1);
            DisallowButton(EventPlayer(), Button.Ability2);
            DisallowButton(EventPlayer(), Button.PrimaryFire);
            DisallowButton(EventPlayer(), Button.SecondaryFire);
            SetMoveSpeed(EventPlayer(), 0);
        }
        else {
            AllowButton(EventPlayer(), Button.Ability1);
            AllowButton(EventPlayer(), Button.Ability2);
            AllowButton(EventPlayer(), Button.PrimaryFire);
            AllowButton(EventPlayer(), Button.SecondaryFire);
        }
        
        //Roach logic
        if (IsDummyBot(EventPlayer()) && EventPlayer().Hero() == Hero.Tracer && IsGameInProgress()) {
            define direction = Vector(RandomReal(-100, 100), 0, RandomReal(-100, 100)).Normalize();
            SetFacing(EventPlayer(), ClosestPlayerTo(EventPlayer(), Team.Team1), Relative.ToWorld);
            StartThrottleInDirection(EventPlayer(), direction, 100, Relative.ToPlayer, ThrottleBehavior.AddToExistingThrottle);

            if (RandomReal(0,1) < 0.75) {
                PressButton(EventPlayer(), Button.Ability1);
            }
            else if (!IsJumping()) {
                PressButton(EventPlayer(), Button.Jump);
            }
            
        }

        //Pharah logic
        if (IsDummyBot(EventPlayer()) && EventPlayer().Hero() == Hero.Pharah && IsGameInProgress()) {
            define direction = Vector(RandomReal(-100, 100), 0, RandomReal(-100, 100)).Normalize();
            SetFacing(EventPlayer(), ClosestPlayerTo(EventPlayer(), Team.Team1), Relative.ToWorld);
            StartThrottleInDirection(EventPlayer(), direction, 100, Relative.ToPlayer, ThrottleBehavior.AddToExistingThrottle);

            if (RandomReal(0,1) < 0.75) {
                PressButton(EventPlayer(), Button.Ability1);
            }
            if (RandomReal(0,1) < 0.75) {
                StartHoldingButton(EventPlayer(), Button.Jump);
            }
            else {
                StopHoldingButton(EventPlayer(), Button.Jump);
            }
        }


        // Reinhardt logic
        if (IsDummyBot(EventPlayer()) && EventPlayer().Hero() == Hero.Reinhardt && IsGameInProgress()) {
            if (CountOf(FilteredArray(AllLivingPlayers(Team.Team2), HasSpawned(ArrayElement()))) > 0) {
                define closestPlayer = FirstOf(SortedArray(FilteredArray(AllLivingPlayers(Team.Team2), HasSpawned(ArrayElement())), DistanceBetween(EventPlayer(), ArrayElement())));
                define timeToPlayer = DistanceBetween(EyePosition(EventPlayer()), EyePosition(closestPlayer))/ (26.66*5); // 500% projectile speed
                define nextPlayerPos = EyePosition(closestPlayer) +  VelocityOf(closestPlayer)*timeToPlayer;
                nextPlayerPos += Vector(RandomReal(-rein_inaccuracy_range, rein_inaccuracy_range), RandomReal(-rein_inaccuracy_range, rein_inaccuracy_range), RandomReal(-rein_inaccuracy_range, rein_inaccuracy_range));
                //nextPlayerPos = Vector(nextPlayerPos.X, EyePosition(closestPlayer).Y, nextPlayerPos.Z);
                    
                //SetFacing(EventPlayer(), DirectionTowards(EyePosition(EventPlayer()), EyePosition(closestPlayer)), Relative.ToWorld);
                SetFacing(EventPlayer(), DirectionTowards(EyePosition(EventPlayer()), nextPlayerPos), Relative.ToWorld); // Aim prediction?

                if (AbsoluteValue(PositionOf(EventPlayer()).Y - PositionOf(closestPlayer).Y) <= 4.5) {
                    PressButton(EventPlayer(), Button.Ability1);
                }
                    
                PressButton(EventPlayer(), Button.Ability1);
                define distToPlayer = DistanceBetween(EventPlayer(), closestPlayer);
                if (distToPlayer > 2 ) {
                    //StartThrottleInDirection(EventPlayer(), DirectionTowards(EventPlayer(), closestPlayer), 100, Relative.ToWorld, ThrottleBehavior.AddToExistingThrottle);
                    StartThrottleInDirection(EventPlayer(), DirectionTowards(EventPlayer(), nextPlayerPos), 100, Relative.ToWorld, ThrottleBehavior.AddToExistingThrottle);
                }
                else {
                    StopThrottleInDirection(EventPlayer());
                }            
                if (distToPlayer <= 5) {
                    PressButton(EventPlayer(), Button.PrimaryFire);
                }
                else {
                    PressButton(EventPlayer(), Button.Ability2);
                }
            }
            else {
                StopThrottleInDirection(EventPlayer());
            }
        }
        Wait(0.25);
    }
}

rule: "Rein_crouch"
Event.OngoingPlayer {
    for (;;) {

        //Reinhardt tbag
        if (IsDummyBot(EventPlayer()) && EventPlayer().Hero() == Hero.Reinhardt) {
            if (CountOf(AllLivingPlayers(Team.Team2)) == 0 || !IsGameInProgress()) {
                if (!IsCrouching(EventPlayer())) {
                    StartHoldingButton(EventPlayer(), Button.Crouch);
                }
                else {
                    StopHoldingButton(EventPlayer(), Button.Crouch);
                }
            }
            else if (IsCrouching(EventPlayer())) {
                StopHoldingButton(EventPlayer(), Button.Crouch);
            }
        }
        
        Wait(0.25);
    }
}

rule: "Red explosion on death"
Event.OnDeath {
    if (EventPlayer().Team() == Team.Team2) {
        //CreateEffect(AllPlayers(), Effect.DecalSound, Color.Red, EventPlayer(), 10);
        PlayEffect(AllPlayers(), PlayEffect.BadExplosion, Color.Red, EventPlayer(), 20.0);
    }
}